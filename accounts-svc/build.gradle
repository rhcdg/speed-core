buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath('org.owasp:dependency-check-gradle:6.1.1')
    }
}

plugins {
    id 'java'
    id 'eclipse'
    id 'org.springframework.boot' version '2.3.9.RELEASE'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'jacoco'
    id 'org.sonarqube' version '3.1.1'
    id 'org.liquibase.gradle' version '2.0.4'
    id 'checkstyle'
    id 'io.freefair.lombok' version '5.3.0'
}

if(JavaVersion.current() != JavaVersion.VERSION_11){
    throw new GradleException("This build must be run with Java 11. Currently running Java ${JavaVersion.current()}")
}

apply plugin: 'org.owasp.dependencycheck'

dependencyCheck {
    suppressionFile = "owasp.suppression.xml"
    failBuildOnCVSS = 4
}

ext['tomcat.version'] = '9.0.43'

group = 'net.steampunkfoundry.techdemo'
version = '0.0.1-SNAPSHOT'
java.sourceCompatibility = JavaVersion.VERSION_11
java.targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
    maven {
        url = System.env.SPEED_REPO_URL
        
        credentials {
            username System.env.SPEED_REPO_USER 
            password System.env.SPEED_REPO_PASSWORD
        }
    }
}

sourceSets {
    itest {
        java.srcDir file('src/itest/java')
        resources.srcDir file('src/itest/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

sonarqube {
    properties {
        property 'sonar.coverage.exclusions', '**/*Application.java,**/*Config.java'
        property 'sonar.zaproxy.reportPath', 'reports/zap.xml'
        property 'sonar.zaproxy.htmlReportPath', 'reports/zap.html'
    }
}

task itest(type: Test) {
    testClassesDirs = sourceSets.itest.output.classesDirs
    classpath = sourceSets.itest.runtimeClasspath
}

jacocoTestReport.dependsOn jacocoTestCoverageVerification
test.finalizedBy jacocoTestReport
itest.finalizedBy jacocoTestReport

check.dependsOn itest
itest.mustRunAfter test

jacoco {
    toolVersion = "0.8.5"
    reportsDir = file("${buildDir}/reports/jacoco")
}

jacocoTestReport {
    dependsOn test
    executionData(test)
    reports {
        xml.enabled true
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
}

jacocoTestCoverageVerification {
    dependsOn test
    executionData(test)
    violationRules {
        rule {
            element = 'CLASS'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.90
            }
            // Exclude main app file from coverage verification
            excludes = [
                'net.steampunkfoundry.techdemo.personservice.AccountServiceApplication',
                'net.steampunkfoundry.techdemo.personservice.config.security.SecurityConfig'
                ]
        }
        rule {
            element = 'CLASS'
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 1.0
            }
        }
    }
}

configurations {
    itestCompile.extendsFrom testCompile
    itestRuntime.extendsFrom testRuntime
}

checkstyle {
    toolVersion '8.34'
    configFile file('config/checkstyle/google_checks.xml')
    maxWarnings = 0
}

def changeLog  = '$projectDir/src/main/resources/db/changelog/db.changelog-master.yaml'
def dbEndpoint = System.getenv('USERS_POSTGRES_ENDPOINT')
def dbPort = System.getenv('USERS_POSTGRES_PORT')
def dbName = System.getenv('USERS_POSTGRES_DB')
def dbUser = System.getenv('USERS_POSTGRES_USER')
def dbPassword = System.getenv('USERS_POSTGRES_PASSWORD')

liquibase {
    activities {
        main {
            changeLogFile changeLog
            url 'jdbc:postgresql://' + dbEndpoint + ':' + dbPort + '/' + dbName
            username dbUser
            password dbPassword
            logLevel 'debug'
        }
    }
}

bootRun {
    systemProperties System.properties
}

dependencies {
    implementation enforcedPlatform('net.steampunkfoundry.techdemo:td-bom:1.0.6')

    implementation 'io.springfox:springfox-data-rest'
    implementation 'io.springfox:springfox-swagger-ui'
    implementation 'io.springfox:springfox-swagger2'
    implementation 'javax.validation:validation-api'
    implementation 'org.hibernate:hibernate-envers'
    implementation 'org.owasp.encoder:encoder'
    implementation 'org.projectlombok:lombok'

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-rest'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    
    implementation('org.apache.tomcat.embed:tomcat-embed-core') {
        version {
            strictly '9.0.43'
        }
    }
    implementation('org.apache.tomcat.embed:tomcat-embed-websocket') {
        version {
            strictly '9.0.43'
        }
    }
	
    implementation('org.apache.tomcat:tomcat-annotations-api') {
        version {
            strictly '9.0.43'
        }
    }

    runtimeOnly 'org.liquibase:liquibase-core'
    runtimeOnly 'org.postgresql:postgresql'
    testImplementation 'com.h2database:h2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
}
